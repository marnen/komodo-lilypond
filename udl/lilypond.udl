# Main lex file for Lilypond markup.
sublanguage Lilypond
family markup

start_style CSL_DEFAULT
end_style CSL_REGEX

initial IN_CSL_DEFAULT

pattern IDENTIFIERCHAR = '[^\W\d]' # characters that can appear in identifiers
pattern NONIDENTIFIERCHAR = '[\W\d]'
pattern NUMBERCHAR = '[\d\.]'
pattern NONNUMBERCHAR = '[^\d\.]'

state IN_CSL_DEFAULT:
'%{' : paint(upto, CSL_DEFAULT), => IN_CSL_COMMENTBLOCK
'%' : paint(upto, CSL_DEFAULT), => IN_CSL_COMMENT
'"' : paint(upto, CSL_DEFAULT), => IN_CSL_STRING
/\\(?=$IDENTIFIERCHAR)/ : paint(upto, CSL_DEFAULT), paint(include, CSL_IDENTIFIER), => IN_CSL_IDENTIFIER
/\d/ : paint(upto, CSL_DEFAULT), redo, => IN_CSL_NUMBER

state IN_CSL_COMMENTBLOCK:
'%}' : paint(include, CSL_COMMENTBLOCK), => IN_CSL_DEFAULT
/\z/ : paint(upto, CSL_COMMENTBLOCK)

state IN_CSL_COMMENT:
/\n/ : paint(include, CSL_COMMENT), => IN_CSL_DEFAULT

state IN_CSL_STRING:
'\\"' : #stay
'"' : paint(include, CSL_STRING), => IN_CSL_DEFAULT

state IN_CSL_NUMBER:
/$NONNUMBERCHAR/ : paint(upto, CSL_NUMBER), => IN_CSL_DEFAULT

state IN_CSL_IDENTIFIER:
/$NONIDENTIFIERCHAR/ : paint(upto, CSL_IDENTIFIER), redo, => IN_CSL_DEFAULT